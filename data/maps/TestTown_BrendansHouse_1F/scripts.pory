mapscripts TestTown_BrendansHouse_1F_MapScripts{
	MAP_SCRIPT_ON_LOAD: TestTown_BrendansHouse_1F_OnLoad
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TESTTOWN_INTRO_STATE, 2: TestTown_BrendansHouse_1F_OnFrame
		VAR_TESTTOWN_INTRO_STATE, 5: TestTown_BrendansHouse_1F_OnFrame2
    ]
}

script TestTown_BrendansHouse_1F_OnFrame{
	lockall
	playse(SE_PIN)
	applymovement(1, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(1, Common_Movement_Delay48)
	waitmovement(0)
	applymovement(1, Movement_MomGetsUp)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
	applymovement(1, Movement_MomGetToPlayer)
	waitmovement(0)
	msgbox(Mom_GoGetMay)
	closemessage
	applymovement(1, Movement_MomGoesToSink)
	waitmovement(0)
	setvar(VAR_TESTTOWN_INTRO_STATE, 3)
	releaseall
}

script TestTown_BrendansHouse_Mom {
	lockall
	faceplayer
	if (var(VAR_TESTTOWN_INTRO_STATE) == 3) {
		msgbox(Mom_MayIsNextDoor, MSGBOX_NPC)
	}
	elif (!checkitem(ITEM_OLD_ROD)) {
		msgbox(TestTown_Text_GiveDadsRod)
		giveitem(ITEM_OLD_ROD)
		setflag(FLAG_RECEIVED_OLD_ROD)
		msgbox(TestTown_Text_ThrowInFishingAdvice, MSGBOX_NPC)
	}
	else {
		msgbox(TestTown_Mom_IhopeYoureWell, MSGBOX_NPC)
	}
	closemessage
	releaseall
}

text TestTown_Text_GiveDadsRod {
	format("Hi {PLAYER}!\p I love your new companion-It looks full of energy!\p I found Dad's old fishing rod in a box upstairs.\p I don't think he would mind me giving it to you.")
}

text TestTown_Text_ThrowInFishingAdvice {
	format("Here's how it works.\p First, you want to face the water, then use the rod.\p If you get a bite, pull on the rod.\p Sometimes you can snag something immediately, but with bigger catches, you need to time the pulls on your rod to haul them in.\p Please be careful.\p If anything happens, you can come home.\p Go on, go get them, honey!")
}

text TestTown_Mom_IhopeYoureWell {
	format("Hi {PLAYER}!\p I hope your adventure is everything you had hoped it would be.")
}

script TestTown_MaysHouse_Azurill {
	lockall
	faceplayer
	waitse
	playmoncry(SPECIES_AZURILL, CRY_MODE_NORMAL)
	waitmoncry
	applymovement(2, Common_Movement_FaceOriginalDirection)
	releaseall
}

movement Movement_MomGetsUp {
	walk_up * 3
}

movement Movement_MomGetToPlayer {
	walk_right * 2
}

movement Movement_MomGoesToSink {
	walk_left * 6
	walk_in_place_faster_up
}

text Mom_GoGetMay {
    format("Mom: Hi Sweetie! I am so excited for you, I can barely stay in place!\p May is also getting her first Pokémon from Dad today.\p You should fetch her so you can all go together to Dad's gym.\p Also, don't forget to come see me before your big adventure begins.\p Have fun!")
}

text Mom_MayIsNextDoor {
	format("You should go next door to see May.\p Tell her mom I said hello!")
}

script TestTown_BrendansHouse_1F_OnLoad {
	if (var(VAR_TESTTOWN_INTRO_STATE) == 5) {
		setobjectxyperm(3, 9, 5)
		setobjectmovementtype(3, MOVEMENT_TYPE_FACE_LEFT)
	}
	else {
		setobjectxyperm(3, 2, 7)
		setobjectmovementtype(3, MOVEMENT_TYPE_FACE_RIGHT)
	}
}

script TestTown_BrendansHouse_1F_OnFrame2 {
	lockall
	delay(10)
	msgbox(RivalMomToBrendan_BeSafe)
	closemessage
	delay(10)
	playse(SE_PIN)
	applymovement(4, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(4, Common_Movement_Delay48)
	waitmovement(0)
	applymovement(3, Common_Movement_WalkInPlaceFasterDown)
	applymovement(4, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	delay(10)
	applymovement(4, RivalBrendan_ApproachesPlayer)
	waitmovement(0)
	msgbox(RivalBrendanToPlayer_CantWaitLetsGo)
	closemessage
	delay(10)
	setflag(FLAG_DONT_TRANSITION_MUSIC)
	playbgm(MUS_FOLLOW_ME, FALSE)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	applymovement(4, Rival_ExitsBrendansHouse)
	waitmovement(0)
	removeobject(4)
	setvar(VAR_TESTTOWN_INTRO_STATE, 6)
	setflag(FLAG_HIDE_RIVAL_BRENDANS_HOUSE)
	warp(MAP_TEST_TOWN, 5, 8)
	waitstate
	releaseall
}

movement RivalBrendan_ApproachesPlayer {
	walk_down * 2
}

movement Rival_ExitsBrendansHouse {
	walk_right
	walk_down
}

text RivalMomToBrendan_BeSafe {
	format("Brendan's mom: Please, be safe out there.")
}

text RivalBrendanToPlayer_CantWaitLetsGo {
	format("Brendan: Hi {PLAYER}!\p I can't wait anymore to meet my first Pokémon!\p I've been waiting for this day for so long!\p Let's not waste another second and head straight to the gym!")
}

script TestTown_BrendansHouse_1F_RivalMom {
	msgbox(TestTown_BrendansHouse_1F_RivalMom_Text, MSGBOX_NPC)
}

text TestTown_BrendansHouse_1F_RivalMom_Text {
	format("Hello {PLAYER}!\p Thank you for being such a good friend to Brendan.\p It means a lot to me.")
}
