raw `
.set LOCALID_AIDE1, 1
.set LOCALID_AIDE2, 4
.set LOCALID_BIRCH, 2
.set LOCALID_MAY, 3
.set LOCALID_BRENDAN, 5
.set LOCALID_WALLY, 6
`

mapscripts LittlerootTown_ProfessorBirchsLab_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: GladehavenTown_ProfessorBirchsLab_OnTransition
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_BIRCH_LAB_STATE, 0: GladehavenTown_ProfessorBirchsLab_OnFrame
    ]
}

script GladehavenTown_ProfessorBirchsLab_OnTransition {
	if (var(VAR_BIRCH_LAB_STATE) == 0) {
		checkplayergender
		if (var(VAR_RESULT) == MALE) {
			clearflag(FLAG_HIDE_GLADEHAVEN_TOWN_MAY)
		}
		else {
			clearflag(FLAG_HIDE_GLADEHAVEN_TOWN_BRENDAN)
		}
		clearflag(FLAG_HIDE_GLADEHAVEN_TOWN_WALLY)
	}
}

script GladehavenTown_ProfessorBirchsLab_OnFrame {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, GladehavenTown_ProfessorBirchsLab_Movement_PlayerEnterLabForPokedex)
	waitmovement(0)
	msgbox(GladehavenTown_ProfessorBirchsLab_Text_HeardYouBeatRivalTakePokedex)
	playfanfare(MUS_OBTAIN_ITEM)
	message(GladehavenTown_ProfessorBirchsLab_Text_ReceivedPokedex)
	waitfanfare
	setflag(FLAG_SYS_POKEDEX_GET)
	special(SetUnlockedPokedexFlags)
	setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
	setflag(FLAG_ADVENTURE_STARTED)
	setflag(FLAG_SYS_NATIONAL_DEX)
	special(EnableNationalPokedex)
	msgbox(GladehavenTown_ProfessorBirchsLab_Text_ExplainPokedex)
	giveitem(ITEM_RARE_CANDIES)
	giveitem(ITEM_KEY_REPEL)
	closemessage
	checkplayergender
	if (var(VAR_RESULT) == MALE) {
		msgbox(GladehavenTown_ProfessorBirchsLab_Text_MayGotPokedexTooTakeThese)
		giveitem(ITEM_POKE_BALL, 10)
		msgbox(GladehavenTown_ProfessorBirchsLab_Text_CatchCutePokemonWithPokeBalls)
	}
	elif (var(VAR_RESULT) == FEMALE) {
		msgbox(GladehavenTown_ProfessorBirchsLab_Text_BrendanGotPokedexTooTakeThese)
		giveitem(ITEM_POKE_BALL, 10)
		msgbox(GladehavenTown_ProfessorBirchsLab_Text_CatchCoolPokemonWithPokeBalls)
	}
	closemessage
	setvar(VAR_BIRCH_LAB_STATE, 1)
	setvar(VAR_ROUTE102_STATE, 0)
	setflag(FLAG_HIDE_GLADEHAVEN_TOWN_WALLY)
	setflag(FLAG_HIDE_GLADEHAVEN_TOWN_MAY)
	setflag(FLAG_HIDE_GLADEHAVEN_TOWN_BRENDAN)
	setflag(FLAG_HIDE_GLADEHAVEN_TOWN_PROFAIDE)
	releaseall
}

movement GladehavenTown_ProfessorBirchsLab_Movement_PlayerEnterLabForPokedex {
	walk_up * 6
}

text GladehavenTown_ProfessorBirchsLab_Text_HeardYouBeatRivalTakePokedex {
	format("Prof. Birch: Oh, hi, Wally and {PLAYER}!\p I heard you already had your first battle a couple of minutes ago. That's great!\p {RIVAL}'s been helping with my research for a while now.\p Even if {RIVAL} just had their first Pokémon, they know a lot about them.\p Wally, {PLAYER}, I ordered this for my research and I think you should both have it.\p The Pokédex will help you as trainers and will help with my ongoing research on Pokémon.")
}

text GladehavenTown_ProfessorBirchsLab_Text_ReceivedPokedex {
	format("{PLAYER} received the Pokédex!")
}

text GladehavenTown_ProfessorBirchsLab_Text_ExplainPokedex {
	format("Prof. Birch: The Pokédex is a high-tech tool that automatically makes a record of any Pokémon you meet or catch.\p My kid, {RIVAL}, goes everywhere with it.\p Whenever my kid catches a rare Pokémon and records its data in the Pokédex, why, {RIVAL} looks for me while I'm out doing fieldwork, and shows me.\p Oh! Before I forget.\n Please take these items.")
}

text GladehavenTown_ProfessorBirchsLab_Text_MayGotPokedexTooTakeThese {
	format("May: Oh, wow, you guys!\n You got a Pokédex, too!\p That's great! Just like me!\n I've got something for you, too!")
}

text GladehavenTown_ProfessorBirchsLab_Text_CatchCutePokemonWithPokeBalls {
	format("May: It's fun if you can get a lot of Pokémon!\p I'm going to look all over the place because I want different Pokémon.\p Wally, {PLAYER}, let's meet in route 102.\p I'll teach you how to catch a Pokémon with a Poké ball.")
}

text GladehavenTown_ProfessorBirchsLab_Text_BrendanGotPokedexTooTakeThese {
	format("Brendan: Huh!\n So you got a Pokédex, too.\p Well then, here.\n You should have these.")
}

text GladehavenTown_ProfessorBirchsLab_Text_CatchCoolPokemonWithPokeBalls {
	format("Brendan: You know it's more fun to have a whole bunch of Pokémon.\p I'm going to explore all over the place to find different Pokémon.\p Wally, {PLAYER}, let's meet in route 102.\p I'll teach you how to catch a Pokémon with a Poké ball.")
}

script GladehavenTown_ProfessorBirchsLab_EventScript_Birch {
	lock
	faceplayer
	goto(ProfBirch_EventScript_RatePokedexOrRegister)
	msgbox(GladehavenTown_ProfessorBirchsLab_Text_CountlessPokemonAwaitting)
	release
}

script GladehavenTown_ProfessorBirchsLab_EventScript_Rival {
	msgbox(GladehavenTown_ProfessorBirchsLab_Text_MeetUsInRoute102, MSGBOX_NPC)
}

text GladehavenTown_ProfessorBirchsLab_Text_MeetUsInRoute102 {
	format("Meet me and Wally in route 102.\n We will learn how to catch Pokémon.")
}

script GladehavenTown_ProfessorBirchsLab_EventScript_Wally {
	msgbox(GladehavenTown_ProfessorBirchsLab_Text_Wally, MSGBOX_NPC)
}

text GladehavenTown_ProfessorBirchsLab_Text_Wally {
	format("You've all shown me nothing but generosity!\p I don't know how will repay any of you...\p I know!\n I'll start by filling my Pokédex as much as possible!")
}

text GladehavenTown_ProfessorBirchsLab_Text_CountlessPokemonAwaitting {
	format("Prof. Birch: Countless Pokémon await you!\p Argh, I'm getting the itch to get out and do fieldwork again!")
}

script LittlerootTown_ProfessorBirchsLab_EventScript_Machine {
	msgbox(LittlerootTown_ProfessorBirchsLab_Text_SeriousLookingMachine, MSGBOX_SIGN)
}

script LittlerootTown_ProfessorBirchsLab_EventScript_PC {
	msgbox(LittlerootTown_ProfessorBirchsLab_Text_PCUsedForResearch, MSGBOX_SIGN)
}

script LittlerootTown_ProfessorBirchsLab_EventScript_Bookshelf {
	msgbox(LittlerootTown_ProfessorBirchsLab_Text_CrammedWithBooksOnPokemon, MSGBOX_SIGN)
}

script LittlerootTown_ProfessorBirchsLab_EventScript_Book {
	msgbox(LittlerootTown_ProfessorBirchsLab_Text_BookTooHardToRead, MSGBOX_SIGN)
}

script GladehavenTown_ProfessorBirchsLab_EventScript_Aide1 {
	msgbox(GladehavenTown_ProfessorBirchsLab_Text_BirchIsntOneForDeskWork, MSGBOX_NPC)
}

text GladehavenTown_ProfessorBirchsLab_Text_BirchIsntOneForDeskWork {
	format("The professor isn't one for doing desk work.\p He's the type of person who would rather go outside and experience things than read about them here.")
}

script GladehavenTown_ProfessorBirchsLab_EventScript_Aide2 {
	msgbox(GladehavenTown_ProfessorBirchsLab_Text_BirchEnjoysRivalsHelpToo, MSGBOX_NPC)
}

text GladehavenTown_ProfessorBirchsLab_Text_BirchEnjoysRivalsHelpToo {
	format("Prof. Birch is studying the habitats and distribution of Pokémon.\p The Prof enjoys {RIVAL}'s help, too.\n There's a lot of love there.")
}

text LittlerootTown_ProfessorBirchsLab_Text_SeriousLookingMachine {
	format("It's a serious-looking machine.\n The Prof must use this for research.")
}

text LittlerootTown_ProfessorBirchsLab_Text_PCUsedForResearch {
	format("It's a PC used for research.\n Better not mess around with it.")
}

text LittlerootTown_ProfessorBirchsLab_Text_CrammedWithBooksOnPokemon {
	format("It's crammed with books on Pokémon.")
}

text LittlerootTown_ProfessorBirchsLab_Text_BookTooHardToRead {
	format("It's a book that's too hard to read.")
}
