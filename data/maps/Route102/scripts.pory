mapscripts Route102_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: Route102_OnTransition
}

script Route102_OnTransition {
	if (var(VAR_ROUTE102_STATE) == 0) {
		checkplayergender
		if (var(VAR_RESULT) == MALE) {
			clearflag(FLAG_HIDE_ROUTE_102_MAY)
		}
		else {
			clearflag(FLAG_HIDE_ROUTE_102_BRENDAN)
		}
	}
}

script Route102_CatchingTutorial_EventScript {
	lockall
	checkplayergender
	delay(10)
	playse(SE_PIN)
	if (var(VAR_RESULT) == MALE) {
		applymovement(5, Common_Movement_ExclamationMark)
		waitmovement(0)
		applymovement(5, Common_Movement_Delay48)
		waitmovement(0)
		applymovement(5, Common_Movement_WalkInPlaceFasterUp)
		applymovement(4, Common_Movement_WalkInPlaceFasterUp)
		waitmovement(0)
		delay(10)
	}
	else {
		applymovement(6, Common_Movement_ExclamationMark)
		waitmovement(0)
		applymovement(6, Common_Movement_Delay48)
		waitmovement(0)
		applymovement(6, Common_Movement_WalkInPlaceFasterUp)
		applymovement(4, Common_Movement_WalkInPlaceFasterUp)
		waitmovement(0)
		delay(10)
	}
	msgbox(Route102_RivalGreetsPlayer_Text)
	closemessage
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	switch (var(VAR_TEMP_0)) {
		case 13:
			applymovement(OBJ_EVENT_ID_PLAYER, Route102_PlayerToRival13)
		case 14:
			applymovement(OBJ_EVENT_ID_PLAYER, Route102_PlayerToRival14)
		case 15:
			applymovement(OBJ_EVENT_ID_PLAYER, Route102_PlayerToRival15)
		case 16:
			applymovement(OBJ_EVENT_ID_PLAYER, Route102_PlayerToRival16)
		case 17:
			applymovement(OBJ_EVENT_ID_PLAYER, Route102_PlayerToRival17)
	}
	waitmovement(0)
	applymovement(4, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	msgbox(Route102_FirstStepIs_Text)
	closemessage
	special(SavePlayerParty)
	special(LoadWallyZigzagoon)
	special(StartWallyTutorialBattle)
	waitstate
	special(LoadPlayerParty)
	checkplayergender
	if (var(VAR_RESULT) == MALE) {
		applymovement(5, Common_Movement_WalkInPlaceFasterLeft)
	}
	else {
		applymovement(6, Common_Movement_WalkInPlaceFasterLeft)
	}
	msgbox(Route102_IDidIt_Text)
	waitmessage
	checkplayergender
	if (var(VAR_RESULT) == MALE) {
		applymovement(5, Common_Movement_WalkInPlaceFasterUp)
	}
	else {
		applymovement(6, Common_Movement_WalkInPlaceFasterUp)
	}
	msgbox(Route102_YoushouldToo_Text)
	closemessage
	if (var(VAR_RESULT) == MALE) {
		applymovement(5, Route102_RivalTakesOff)
	}
	else {
		applymovement(6, Route102_RivalTakesOff)
	}
	waitmovement(0)
	removeobject(5)
	removeobject(6)
	msgbox(Route102_IllFollowRival_Text)
	closemessage
	applymovement(4, Route102_WallyTakesOff)
	waitmovement(0)
	removeobject(4)
	setvar(VAR_ROUTE102_STATE, 1)
	setflag(FLAG_HIDE_ROUTE_102_BRENDAN)
	setflag(FLAG_HIDE_ROUTE_102_MAY)
	setflag(FLAG_HIDE_ROUTE_102_WALLY)
	releaseall
}

movement Route102_PlayerToRival13 {
	walk_down
	walk_right * 2
	walk_in_place_faster_down
}

movement Route102_PlayerToRival14 {
	walk_down
	walk_right
	walk_in_place_faster_down
}

movement Route102_PlayerToRival15 {
	walk_down
}

movement Route102_PlayerToRival16 {
	walk_down
	walk_left
	walk_in_place_faster_down
}

movement Route102_PlayerToRival17 {
	walk_down
	walk_left * 2
	walk_in_place_faster_down
}

movement Route102_RivalTakesOff {
	walk_down
	walk_left * 8
}

movement Route102_WallyTakesOff {
	walk_left * 7
}

text Route102_RivalGreetsPlayer_Text {
	format("{RIVAL}: {PLAYER}! You are just in time to learn how to catch a Pokémon.\p I was just going to teach Wally.\l Come!")
}

text Route102_FirstStepIs_Text {
	format("{RIVAL}: Ok! The first step is to find a wild Pokémon.\p And then...")
}

text Route102_IDidIt_Text {
	format("Wally: I did it!\n I wasn't sure I could do it, but here I am!\p I caught a Pokémon by myself!\p {RIVAL}: Congrats! You did well.\p What will you do next?\p Wally: I think I'm going to go to the Pokémon trainer school in Jello City.\p I want to know more about Pokémon battling before I try it out for myself.\p {RIVAL}: Good idea! I heard there's a Pokémon gym in Jello City.\p I think I will try to challenge the gym leader for a badge.")
}

text Route102_YoushouldToo_Text {
	format("{PLAYER}, you should totally do that too if you want to become a stronger trainer!\p See ya!")
}

text Route102_IllFollowRival_Text {
	format("Wally: I'll follow {RIVAL} to Jello city just to be safe.\p Bye!")
}

script Route102_Aston_Eventscript {
	trainerbattle_single(TRAINER_ASTON, Route102_Aston_Intro, Route102_Aston_Defeated)
	msgbox(Route102_Aston_PostBattle)
	closemessage
}

text Route102_Aston_Intro {
	format("Hey! I want to show off the Pokémon I caught earlier in the woods!")
}

text Route102_Aston_Defeated {
	format("You didn't have to go this hard...")
}

text Route102_Aston_PostBattle {
	format("Welp, these are the Pokémon I caught!")
}

script Route102_Julia_Eventscript {
	trainerbattle_single(TRAINER_JULIA, Route102_Julia_Intro, Route102_Julia_Defeated)
	msgbox(Route102_Julia_PostBattle)
	closemessage
}

text Route102_Julia_Intro {
	format("What's your favorite kind of Berry? I'll tell you mine if you win this battle.")
}

text Route102_Julia_Defeated {
	format("You win! I guess I have to tell you my favorite kind of Berry now.")
}

text Route102_Julia_PostBattle {
	format("My favorites are Oran Berries!")
}

script Route102_BerryMan_EventScript {
	msgbox(Route102_BerryMan_Text, MSGBOX_NPC)
}

text Route102_BerryMan_Text {
	format("Not_done")
}

script Route102_Runner_EventScript {
	msgbox(Route102_Runner_Text, MSGBOX_NPC)
}

text Route102_Runner_Text {
	format("I'd gladly battle you, but today is training day.")
}