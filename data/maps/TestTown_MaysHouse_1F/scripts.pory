mapscripts TestTown_MaysHouse_1F_MapScripts {
	MAP_SCRIPT_ON_LOAD: TestTown_MaysHouse_1F_OnLoad
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TESTTOWN_INTRO_STATE, 2: TestTown_MaysHouse_1F_OnFrame
		VAR_TESTTOWN_INTRO_STATE, 3: TestTown_MaysHouse_1F_OnFrame2
    ]
}

script TestTown_MaysHouse_1F_OnFrame {
	lockall
	playse(SE_PIN)
	applymovement(1, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(1, Common_Movement_Delay48)
	waitmovement(0)
	applymovement(1, Movement_MomGetsUp_May)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
	applymovement(1, Movement_MomGetToPlayer_May)
	waitmovement(0)
	msgbox(Mom_GoGetBrendan)
	closemessage
	applymovement(1, Movement_MomGoesToSink_May)
	waitmovement(0)
	setvar(VAR_TESTTOWN_INTRO_STATE, 5)
	releaseall
}

script TestTown_MaysHouse_Mom {
	lockall
	faceplayer
	if (var(VAR_TESTTOWN_INTRO_STATE) == 5) {
		msgbox(Mom_BrendanIsNextDoor, MSGBOX_NPC)
	}
	elif (!checkitem(ITEM_OLD_ROD)) {
		msgbox(TestTown_Text_GiveDadsRod_May)
		giveitem(ITEM_OLD_ROD)
		setflag(FLAG_RECEIVED_OLD_ROD)
		msgbox(TestTown_Text_ThrowInFishingAdvice_May, MSGBOX_NPC)
	}
	else {
		msgbox(TestTown_Mom_IhopeYoureWell_May, MSGBOX_NPC)
	}
	closemessage
	releaseall
}

text TestTown_Text_GiveDadsRod_May {
	format("Hi {PLAYER}!\p I love your new companion-It looks full of energy!\p I found Dad's old fishing rod in a box upstairs.\p I don't think he would mind me giving it to you.")
}

text TestTown_Text_ThrowInFishingAdvice_May {
	format("Here's how it works.\p First, you want to face the water, then use the rod.\p If you get a bite, pull on the rod.\p Sometimes you can snag something immediately, but with bigger catches, you need to time the pulls on your rod to haul them in.\p Please be careful.\p If anything happens, you can come home.\p Go on, go get them, honey!")
}

text TestTown_Mom_IhopeYoureWell_May {
	format("Hi {PLAYER}!\p I hope your adventure is everything you had hoped it would be.")
}

script TestTown_MaysHouse_Skitty {
	lockall
	faceplayer
	waitse
	playmoncry(SPECIES_SKITTY, CRY_MODE_NORMAL)
	waitmoncry
	applymovement(2, Common_Movement_FaceOriginalDirection)
	releaseall
}

movement Movement_MomGetsUp_May {
	walk_up * 3
}

movement Movement_MomGetToPlayer_May {
	walk_left * 2
}

movement Movement_MomGoesToSink_May {
	walk_right * 5
	walk_in_place_faster_up
}

text Mom_GoGetBrendan {
    format("Mom: Hi Sweetie! I am so excited for you, I can barely stay in place!\p Brendan is also getting his first Pokémon from Dad today.\p You should fetch him so you can all go together to Dad's gym.\p Also, don't forget to come see me before your big adventure begins.\p Have fun!")
}

text Mom_BrendanIsNextDoor {
	format("You should go next door to see Brendan.\p Tell his mom I said hello!")
}

script TestTown_MaysHouse_1F_OnLoad {
	if (var(VAR_TESTTOWN_INTRO_STATE) == 3) {
		setobjectxyperm(3, 1, 5)
		setobjectmovementtype(3, MOVEMENT_TYPE_FACE_RIGHT)
	}
	else {
		setobjectxyperm(3, 8, 7)
		setobjectmovementtype(3, MOVEMENT_TYPE_FACE_LEFT)
	}
}

script TestTown_MaysHouse_1F_OnFrame2 {
	lockall
	delay(10)
	msgbox(RivalMomToMay_BeSafe)
	closemessage
	delay(10)
	playse(SE_PIN)
	applymovement(4, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(4, Common_Movement_Delay48)
	waitmovement(0)
	applymovement(3, Common_Movement_WalkInPlaceFasterDown)
	applymovement(4, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	delay(10)
	applymovement(4, Rival_ApproachesPlayer)
	waitmovement(0)
	msgbox(RivalToPlayer_CantWaitLetsGo)
	closemessage
	delay(10)
	setflag(FLAG_DONT_TRANSITION_MUSIC)
	playbgm(MUS_FOLLOW_ME, FALSE)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	applymovement(4, Rival_ExitsMaysHouse)
	waitmovement(0)
	removeobject(4)
	setvar(VAR_TESTTOWN_INTRO_STATE, 4)
	setflag(FLAG_HIDE_RIVAL_MAYS_HOUSE)
	warp(MAP_TEST_TOWN, 10, 8)
	waitstate
	releaseall
}

movement Rival_ApproachesPlayer {
	walk_down * 2
}

movement Rival_ExitsMaysHouse {
	walk_left
	walk_down
}

text RivalMomToMay_BeSafe {
	format("May's mom: Please, be safe out there.")
}

text RivalToPlayer_CantWaitLetsGo {
	format("May: Hi {PLAYER}!\p I can't wait anymore to meet my first Pokémon!\p I've been waiting for this day for so long!\p Let's not waste another second and head straight to the gym!")
}

script TestTown_MaysHouse_1F_RivalMom {
	msgbox(TestTown_MaysHouse_1F_RivalMom_Text, MSGBOX_NPC)
}

text TestTown_MaysHouse_1F_RivalMom_Text {
	format("Hello {PLAYER}!\p Thank you for being such a good friend to May.\p It means a lot to me.")
}
