mapscripts RustboroCity_PokemonSchool_MapScripts {
	
}

script RustboroCity_PokemonSchool_EventScript_Teacher {
	
}

script RustboroCity_PokemonSchool_EventScript_Wally {
	lockall
	if (var(VAR_RUSTBORO_CITY_STATE) == 0) {
		msgbox(RustboroCity_PokemonSchool_Text_Wally_BeforeBattle, MSGBOX_NPC)
	}
	else {
		msgbox(RustboroCity_PokemonSchool_Text_Wally_AfterBattle, MSGBOX_NPC)
	}
	releaseall
}

script RustboroCity_PokemonSchool_EventScript_Roxanne {
	lockall
	faceplayer
	msgbox(RustboroCity_PokemonSchool_Text_Roxanne1)
	msgbox("So, are you ready?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		msgbox("Good, let's start then!")
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
		applymovement(7, Common_Movement_WalkInPlaceFasterDown)
		applymovement(8, Common_Movement_WalkInPlaceFasterDown)
		waitmovement(0)
		msgbox("First up is Dave!")
		closemessage
		applymovement(6, RustboroCity_PokemonSchool_Movement_Dave)
		waitmovement(0)
		trainerbattle_no_intro(TRAINER_DAVE, PokemonSchool_Dave_Defeated)
		applymovement(6, RustboroCity_PokemonSchool_Movement_Dave2)
		waitmovement(0)
		msgbox("Roxanne: Next is Eva!")
		closemessage
		applymovement(2, RustboroCity_PokemonSchool_Movement_Eva)
		waitmovement(0)
		trainerbattle_no_intro(TRAINER_EVA, PokemonSchool_Eva_Defeated)
		applymovement(2, RustboroCity_PokemonSchool_Movement_Eva2)
		waitmovement(0)
		msgbox("Roxanne: And finally, Wally!")
		closemessage
		applymovement(7, RustboroCity_PokemonSchool_Movement_Wally1)
		waitmovement(0)
		msgbox(RustboroCity_PokemonSchool_Text_Wally_HopeICanDoThis)
		if (var(VAR_STARTER_MON) == 0) {
			trainerbattle_no_intro(TRAINER_WALLY_SCHOOL_MUDKIP, PokemonSchool_Wally_Defeated)
		}
		elif (var(VAR_STARTER_MON) == 1) {
			trainerbattle_no_intro(TRAINER_WALLY_SCHOOL_TREECKO, PokemonSchool_Wally_Defeated)
		}
		else {
			trainerbattle_no_intro(TRAINER_WALLY_SCHOOL_TORCHIC, PokemonSchool_Wally_Defeated)
		}
		msgbox(RustboroCity_PokemonSchool_Text_Wally_AfterBattle)
		closemessage
		applymovement(7, RustboroCity_PokemonSchool_Movement_Wally2)
		waitmovement(0)
		applymovement(8, Common_Movement_WalkInPlaceFasterLeft)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
		waitmovement(0)
		msgbox(RustboroCity_PokemonSchool_Text_Roxanne2)
		closemessage
		fadescreen(FADE_TO_BLACK)
		playse(SE_EXIT)
    	delay(10)
    	removeobject(8)
		fadescreen(FADE_FROM_BLACK)
		setflag(FLAG_HIDE_ROXANNE_PKMNSCHOOL)
		setvar(VAR_RUSTBORO_CITY_STATE, 1)
		setflag(FLAG_HIDE_RUSTBOROCITY_GYMGUIDE)
		releaseall
	}
	else {
		msgbox("Ok! Come back when you're ready.")
	}
	closemessage
}

text RustboroCity_PokemonSchool_Text_Roxanne1 {
	format("Roxanne: Hello, my name is Roxanne and I'm this city's gym leader!\p I also tutor at this school when the teacher is not here.\p Oh? You want to challenge me?\p Hmm, let's see...\p I have an idea!\p If you can win three times in a row against my students without healing your Pokémon,\p I will let you challenge me at my gym later!")
}

movement RustboroCity_PokemonSchool_Movement_Dave {
	walk_right * 3
	walk_up * 2
}

text PokemonSchool_Dave_Defeated {
	format("It's not fair, I went first!")
}

movement RustboroCity_PokemonSchool_Movement_Dave2 {
	walk_down * 2
	walk_left * 3
	walk_in_place_fast_up
}

movement RustboroCity_PokemonSchool_Movement_Eva {
	walk_right
	walk_up * 4
}

text PokemonSchool_Eva_Defeated {
	format("You make it seems so easy!")
}

movement RustboroCity_PokemonSchool_Movement_Eva2 {
	walk_down * 4
	walk_left
	walk_in_place_faster_down
}

movement RustboroCity_PokemonSchool_Movement_Wally1 {
	walk_right
	walk_down * 2
	walk_left * 3
	walk_up
}

text RustboroCity_PokemonSchool_Text_Wally_HopeICanDoThis {
	format("Wally: I think I'm ready to take you on, {PLAYER}!")
}

text PokemonSchool_Wally_Defeated {
	format("I was maybe a little overconfident...")
}

movement RustboroCity_PokemonSchool_Movement_Wally2 {
	walk_down
	walk_right * 3
	walk_up * 2
	walk_left
	walk_in_place_fast_up
}

text RustboroCity_PokemonSchool_Text_Roxanne2 {
	format("Roxanne: Congrats! I think it means you're ready to challenge me.\p Come by the gym whenever and I'll give you what you want!")
}

script RustboroCity_PokemonSchool_EventScript_SchoolKidM2 {
	lockall
	msgbox(RustboroCity_PokemonSchool_Text_SchoolKidM2, MSGBOX_NPC)
	releaseall
}

text RustboroCity_PokemonSchool_Text_Wally_BeforeBattle {
	format("Hi {PLAYER}!\p I'm learning all about status conditions that can affect Pokémon!")
}

text RustboroCity_PokemonSchool_Text_Wally_AfterBattle {
	format("Wow! You beat all of us in a row just like that!\p I better go back to studying so I get better at battling.")
}

text RustboroCity_PokemonSchool_Text_SchoolKidM2 {
	format("It can be overwhelming to learn about Pokémon battle, but I think I'm doing great!")
}

script PokemonSchool_Eventscript_Scott {
	lockall
	faceplayer
	if (var(VAR_SCOTT_STATE) == 1) {
		msgbox(PokemonSchool_Text_ScottNextBadge)
	}
	elif (flag(FLAG_MET_SCOTT_RUSTBORO) && flag(FLAG_BADGE01_GET) == TRUE) {
		msgbox(PokemonSchool_Text_ScottAlreadyMetAfterBadge, MSGBOX_YESNO)
		msgbox(PokemonSchool_Text_ScottGivesItem)
		giveitem(ITEM_MASTER_BALL)
		setvar(VAR_SCOTT_STATE, 1)
	}
	elif (flag(FLAG_BADGE01_GET) == TRUE) {
		msgbox(PokemonSchool_Text_ScottFirstTimeMetAfterBadge, MSGBOX_YESNO)
		msgbox(PokemonSchool_Text_ScottGivesItem)
		giveitem(ITEM_MASTER_BALL)
		setvar(VAR_SCOTT_STATE, 1)
		setflag(FLAG_MET_SCOTT_RUSTBORO)
	}
	elif (flag(FLAG_MET_SCOTT_RUSTBORO) == TRUE) {
		msgbox(PokemonSchool_Text_ScottAlreadyMet)
	}
	else {
		msgbox(PokemonSchool_Text_ScottFirstTimeMet)
		setflag(FLAG_MET_SCOTT_RUSTBORO)
	}
	closemessage
	releaseall
}

text PokemonSchool_Text_ScottFirstTimeMet {
	format("Scott: Are you a student here? No?\p Oh! But you're a Pokémon trainer. I can see it in your face.\p My name is Scott and I'm a board member of the Pokémon league.\p Which means I oversee all Pokémon gyms and of course the Pokémon league.\p Recently, I've made some changes to ensure it's not too easy to earn badges.\p So, some gym leaders and elite four members were promoted and some were demoted.\p Also, I made some of them change Pokémon type to balance things out.\p Anyways, it was nice meeting you!")
}

text PokemonSchool_Text_ScottAlreadyMet {
	format("Scott: Are you going to tackle the gym in this city?\p Roxanne is the leader and her specialty is ground-type Pokémon.\p If you challenge her, I'd love to have your opinion on how difficult the battle was.\p I'll maybe even throw you a little gift to thank you.")
}

text PokemonSchool_Text_ScottFirstTimeMetAfterBadge {
	format("Scott: Are you a student here? No?\p Oh! But you're a Pokémon trainer. I can see it in your face.\p My name is Scott and I'm a board member of the Pokémon league.\p Which means I oversee all Pokémon gyms and of course the Pokémon league.\p Recently, I've made some changes to ensure it's not too easy to earn badges.\p So, some gym leaders and elite four members were promoted and some were demoted.\p Also, I made some of them change Pokémon type to balance things out.\p Is that a ground badge?\p Good job! Was the battle with Roxanne too easy?")
}

text PokemonSchool_Text_ScottGivesItem {
	format("I see... Thank you for taking the time.\p The board really appreciates it.\p Anyways, take this as a thank you gift.")
}

text PokemonSchool_Text_ScottAlreadyMetAfterBadge {
	format("Scott: Is that a ground badge?\p Good job! Was the battle with Roxanne too easy?")
}

text PokemonSchool_Text_ScottNextBadge {
	format("Scott: The next badge on your list should be in Dewford town.\p The gym leader, Glacia, is a grass-type Pokémon trainer.")
}

raw `
RustboroCity_PokemonSchool_EventScript_Blackboard::
	lockall
	msgbox RustboroCity_PokemonSchool_Text_BlackboardListsStatusChanges, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic::
	message RustboroCity_PokemonSchool_Text_ReadWhichTopic
	waitmessage
	multichoicegrid 8, 1, MULTI_STATUS_INFO, 3, FALSE
	switch VAR_RESULT
	case 0, RustboroCity_PokemonSchool_EventScript_Poison
	case 1, RustboroCity_PokemonSchool_EventScript_Paralysis
	case 2, RustboroCity_PokemonSchool_EventScript_Sleep
	case 3, RustboroCity_PokemonSchool_EventScript_Burn
	case 4, RustboroCity_PokemonSchool_EventScript_Freeze
	case 5, RustboroCity_PokemonSchool_EventScript_ExitTopicSelect
	case MULTI_B_PRESSED, RustboroCity_PokemonSchool_EventScript_ExitTopicSelect
	end

RustboroCity_PokemonSchool_EventScript_Poison::
	msgbox RustboroCity_PokemonSchool_Text_ExplainPoison, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Paralysis::
	msgbox RustboroCity_PokemonSchool_Text_ExplainParalysis, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Sleep::
	msgbox RustboroCity_PokemonSchool_Text_ExplainSleep, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Burn::
	msgbox RustboroCity_PokemonSchool_Text_ExplainBurn, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Freeze::
	msgbox RustboroCity_PokemonSchool_Text_ExplainFreeze, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_ExitTopicSelect::
	releaseall
	end

RustboroCity_PokemonSchool_EventScript_RichBoy::
	msgbox RustboroCity_PokemonSchool_Text_PokemontCantUseManMadeItems, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_Lass::
	msgbox RustboroCity_PokemonSchool_Text_ConfusedPokemonAttacksItself, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_SchoolKidM::
	msgbox RustboroCity_PokemonSchool_Text_PokemonHealItselfWithBerry, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_StudentNotebook::
	msgbox RustboroCity_PokemonSchool_Text_StudentsNotes, MSGBOX_SIGN
	end

RustboroCity_PokemonSchool_Text_BlackboardListsStatusChanges:
	.string "The blackboard lists status changes\n"
	.string "that may affect Pokémon in battle.$"

RustboroCity_PokemonSchool_Text_ReadWhichTopic:
	.string "Which topic do you want to read?$"

RustboroCity_PokemonSchool_Text_ExplainPoison:
	.string "If a Pokémon is poisoned, it will\n"
	.string "steadily lose HP.\p"
	.string "The effects of poison remain after\n"
	.string "a battle.\p"
	.string "A poisoned Pokémon's HP will drop\n"
	.string "while it is traveling.\p"
	.string "Heal a poisoning using an antidote.$"

RustboroCity_PokemonSchool_Text_ExplainParalysis:
	.string "If a Pokémon becomes paralyzed,\n"
	.string "its speed drops.\p"
	.string "It may also not be able to move while\n"
	.string "it is in battle.\p"
	.string "Paralysis remains after battle.\n"
	.string "Cure it using a paralyze heal.$"

RustboroCity_PokemonSchool_Text_ExplainSleep:
	.string "If a Pokémon falls asleep, it will be\n"
	.string "unable to attack.\p"
	.string "A Pokémon may wake up on its own,\n"
	.string "but if a battle ends while it is\l"
	.string "sleeping, it will stay asleep.\p"
	.string "Wake it up using an awakening.$"

RustboroCity_PokemonSchool_Text_ExplainBurn:
	.string "A burn reduces attack power, and it\n"
	.string "steadily reduces the victim's HP.\p"
	.string "A burn lingers after battle.\n"
	.string "Cure a burn using a burn heal.$"

RustboroCity_PokemonSchool_Text_ExplainFreeze:
	.string "If a Pokémon is frozen, it becomes\n"
	.string "completely helpless.\p"
	.string "It will remain frozen after battle.\n"
	.string "Thaw it out using an ice heal.$"

RustboroCity_PokemonSchool_Text_PokemontCantUseManMadeItems:
	.string "Pokémon can hold items, but they\n"
	.string "don't know what to do with man-made\l"
	.string "items like potion and antidote.$"

RustboroCity_PokemonSchool_Text_ConfusedPokemonAttacksItself:
	.string "You know how some Pokémon moves can\n"
	.string "confuse a Pokémon?\p"
	.string "A confused Pokémon will sometimes\n"
	.string "attack itself without meaning to.\p"
	.string "But once it leaves battle, it will\n"
	.string "return to normal.$"

RustboroCity_PokemonSchool_Text_PokemonHealItselfWithBerry:
	.string "A Pokémon holding a berry will heal\n"
	.string "itself…\p"
	.string "There are many kinds of items that\n"
	.string "Pokémon can hold…\p"
	.string "Boy, it sure is hard taking notes\n"
	.string "down…$"

RustboroCity_PokemonSchool_Text_StudentsNotes:
	.string "It's this student's notebook…\p"
	.string "Pokémon are to be caught using\n"
	.string "Poké balls.\p"
	.string "Up to six Pokémon can accompany\n"
	.string "a trainer.\p"
	.string "A trainer is someone who catches\n"
	.string "Pokémon, raises them, and battles\l"
	.string "with them.\p"
	.string "A trainer's mission is to defeat\n"
	.string "the strong trainers who await\l"
	.string "challengers in Pokémon gyms.$"

`
