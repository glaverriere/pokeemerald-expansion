mapscripts Route101_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: Route101_OnTransition
}

script Route101_OnTransition {
	if (var(VAR_ROUTE101_STATE) == 0) {
		clearflag(FLAG_HIDE_ROUTE101_WALLY)
		checkplayergender
		if (var(VAR_RESULT) == MALE) {
			clearflag(FLAG_HIDE_ROUTE101_MAY)
		}
		else {
			clearflag(FLAG_HIDE_ROUTE101_BRENDAN)
		}
	}
}

script Route101_Intro_Trigger {
	lockall
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	switch (var(VAR_TEMP_0)) {
		case 8:
			applymovement(4, Route101_4ToPlayer8)
		case 9:
			applymovement(4, Route101_4ToPlayer9)
		case 10:
			applymovement(4, Route101_4ToPlayer10)
		case 11:
			applymovement(4, Route101_4ToPlayer11)
		case 12:
			applymovement(4, Route101_4ToPlayer12)
	}
	waitmovement(0)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	msgbox(Route101_ItsDangerous)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Route101_PlayerDown)
	switch (var(VAR_TEMP_0)) {
		case 8:
			applymovement(4, Route101_4Playerto8)
		case 9:
			applymovement(4, Route101_4Playerto9)
		case 10:
			applymovement(4, Route101_4Playerto10)
		case 11:
			applymovement(4, Route101_4Playerto11)
		case 12:
			applymovement(4, Route101_4Playerto12)
	}
	waitmovement(4)
	releaseall
}

movement Route101_4ToPlayer8 {
	walk_right
	walk_in_place_fast_down
}

movement Route101_4ToPlayer9 {
	walk_right * 2
	walk_in_place_fast_down
}

movement Route101_4ToPlayer10 {
	walk_right * 3
	walk_in_place_fast_down
}

movement Route101_4ToPlayer11 {
	walk_right * 4
	walk_in_place_fast_down
}

movement Route101_4ToPlayer12 {
	walk_right * 5
	walk_in_place_fast_down
}

text Route101_ItsDangerous {
	format("It's dangerous to wander in tall grass without a Pokémon.\p I recommend you come back with one.")
}

movement Route101_PlayerDown {
	walk_down
}

movement Route101_4Playerto8 {
	walk_left
	walk_in_place_fast_up
}

movement Route101_4Playerto9 {
	walk_left * 2
	walk_in_place_fast_up
}

movement Route101_4Playerto10 {
	walk_left * 3
	walk_in_place_fast_up
}

movement Route101_4Playerto11 {
	walk_left * 4
	walk_in_place_fast_up
}

movement Route101_4Playerto12 {
	walk_left * 5
	walk_in_place_fast_up
}

script Route101_DudeInGrass {
	giveitem(ITEM_QUICK_BALL, 100)
	giveitem(ITEM_RARE_CANDIES)
	giveitem(ITEM_KEY_REPEL)
	giveitem(ITEM_REPEL)
	msgbox(Route101_Text_DudeInGrass, MSGBOX_NPC)
}

text Route101_Text_DudeInGrass {
	format("Searching for Pokémon in tall grass is so fun!\p The other day, I even found a Pikachu!")
}

script Route101_RivalBattle_Trigger {
	lockall
	checkplayergender
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_RESULT) == MALE) {
		msgbox(Route101_Text_MayVsWally)
		closemessage
		delay(10)
		playse(SE_PIN)
		applymovement(2, Common_Movement_ExclamationMark)
		waitmovement(0)
		applymovement(2, Common_Movement_Delay48)
		waitmovement(0)
		applymovement(2, Common_Movement_WalkInPlaceFasterDown)
		applymovement(1, Common_Movement_WalkInPlaceFasterDown)
		waitmovement(0)
		playbgm(MUS_ENCOUNTER_MAY, TRUE)
		delay(10)
		switch (var(VAR_TEMP_0)) {
			case 6:
				applymovement(2, Route101_RivalToPlayer6)
			case 7:
				applymovement(2, Route101_RivalToPlayer7)
			case 8:
				applymovement(2, Route101_RivalToPlayer8)
			case 9:
				applymovement(2, Route101_RivalToPlayer9)
			case 10:
				applymovement(2, Route101_RivalToPlayer10)
		}
		waitmovement(0)
		msgbox(Route101_Text_LetsBattle)
		switch (var(VAR_STARTER_MON)) {
			case 0:
				trainerbattle_no_intro(TRAINER_MAY_ROUTE_101_TREECKO, Route101_Text_RivalDefeated)
			case 1:
				trainerbattle_no_intro(TRAINER_MAY_ROUTE_101_TORCHIC, Route101_Text_RivalDefeated)
			case 2:
				trainerbattle_no_intro(TRAINER_MAY_ROUTE_101_MUDKIP, Route101_Text_RivalDefeated)
		}
	}
	else {
		msgbox(Route101_Text_BrendanVsWally)
		closemessage
		delay(10)
		playse(SE_PIN)
		applymovement(3, Common_Movement_ExclamationMark)
		waitmovement(0)
		applymovement(3, Common_Movement_Delay48)
		waitmovement(0)
		applymovement(3, Common_Movement_WalkInPlaceFasterDown)
		applymovement(1, Common_Movement_WalkInPlaceFasterDown)
		waitmovement(0)
		playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
		delay(10)
		switch (var(VAR_TEMP_0)) {
			case 6:
				applymovement(3, Route101_RivalToPlayer6)
			case 7:
				applymovement(3, Route101_RivalToPlayer7)
			case 8:
				applymovement(3, Route101_RivalToPlayer8)
			case 9:
				applymovement(3, Route101_RivalToPlayer9)
			case 10:
				applymovement(3, Route101_RivalToPlayer10)
		}
		waitmovement(0)
		msgbox(Route101_Text_LetsBattle)
		switch (var(VAR_STARTER_MON)) {
			case 0:
				trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_101_TREECKO, Route101_Text_RivalDefeated)
			case 1:
				trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_101_TORCHIC, Route101_Text_RivalDefeated)
			case 2:
				trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_101_MUDKIP, Route101_Text_RivalDefeated)
		}
	}
	msgbox(Route101_Text_RivalLetsGoToLab)
	closemessage
	checkplayergender
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_RESULT) == MALE) {
		switch (var(VAR_TEMP_0)) {
			case 6:
				applymovement(2, Route101_RivalToWally6)
			case 7:
				applymovement(2, Route101_RivalToWally7)
			case 8:
				applymovement(2, Route101_RivalToWally8)
			case 9:
				applymovement(2, Route101_RivalToWally9)
			case 10:
				applymovement(2, Route101_RivalToWally10)
		}
	}
	else {
		switch (var(VAR_TEMP_0)) {
			case 6:
				applymovement(3, Route101_RivalToWally6)
			case 7:
				applymovement(3, Route101_RivalToWally7)
			case 8:
				applymovement(3, Route101_RivalToWally8)
			case 9:
				applymovement(3, Route101_RivalToWally9)
			case 10:
				applymovement(3, Route101_RivalToWally10)
		}
	}
	waitmovement(0)
	applymovement(1, Common_Movement_WalkInPlaceFasterUp)
	if (var(VAR_RESULT) == MALE) {
		applymovement(2, Route101_RivalToLab)
	}
	else {
		applymovement(3, Route101_RivalToLab)
	}
	waitmovement(0)
	removeobject(2)
	removeobject(3)
	msgbox("Wally: Wait for me!")
	closemessage
	applymovement(1, Route101_RivalToLab)
	waitmovement(0)
	removeobject(1)
	setflag(FLAG_HIDE_ROUTE101_BRENDAN)
	setflag(FLAG_HIDE_ROUTE101_MAY)
	setflag(FLAG_HIDE_ROUTE101_WALLY)
	setvar(VAR_ROUTE101_STATE, 1)
	playbgm(MUS_ROUTE101, TRUE)
	releaseall
}

text Route101_Text_MayVsWally {
	format("May: Wally! I challenge you to a Pokémon battle!\p Wally: What? Me? A Pokémon battle?\p I don't know how that works.\p May: Don't worry about it.\p I'll challenge {PLAYER} instead.\p Feel free to watch if you would like to learn.")
}

text Route101_Text_BrendanVsWally {
	format("Brendan: Wally! I challenge you to a Pokémon battle!\p Wally: What? Me? A Pokémon battle?\p I don't know how that works.\p Brendan: Don't worry about it.\p I'll challenge {PLAYER} instead.\p Feel free to watch if you would like to learn.")
}

movement Route101_RivalToPlayer6 {
	walk_down * 2
	walk_left * 4
	walk_in_place_faster_down
}

movement Route101_RivalToPlayer7 {
	walk_down * 2
	walk_left * 3
	walk_in_place_faster_down
}

movement Route101_RivalToPlayer8 {
	walk_down * 2
	walk_left * 2
	walk_in_place_faster_down
}

movement Route101_RivalToPlayer9 {
	walk_down * 2
	walk_left
	walk_in_place_faster_down
}

movement Route101_RivalToPlayer10 {
	walk_down * 2
}

text Route101_Text_LetsBattle {
	format("Alright {PLAYER}, let's battle!")
}

text Route101_Text_RivalDefeated {
	format("Wow, {PLAYER}...\n You sure are good at this!")
}

text Route101_Text_RivalLetsGoToLab {
	format("You're a natural!\p Anyways, let's go to my dad's lab.")
}

movement Route101_RivalToWally6 {
	walk_right * 4
	walk_up * 3
}

movement Route101_RivalToWally7 {
	walk_right * 3
	walk_up * 3
}

movement Route101_RivalToWally8 {
	walk_right * 2
	walk_up * 3
}

movement Route101_RivalToWally9 {
	walk_right
	walk_up * 3
}

movement Route101_RivalToWally10 {
	walk_up * 3
}

movement Route101_RivalToLab {
	walk_up * 5
}

script Route101_GirlWalking {
	msgbox(Route101_Text_GirlWalking, MSGBOX_NPC)
}

text Route101_Text_GirlWalking {
	format("Did you know that if you keep walking in tall grass, you'll eventually stumble upon a wild Pokémon?")
}

script Route101_Ronnie_Eventscript {
	trainerbattle_single(TRAINER_RONNIE, Route101_Ronnie_Intro, Route101_Ronnie_Defeated)
	msgbox(Route101_Ronnie_PostBattle)
	closemessage
}

text Route101_Ronnie_Intro {
	format("When two trainers' eyes meet, it means they have to battle!")
}

text Route101_Ronnie_Defeated {
	format("I lost...")
}

text Route101_Ronnie_PostBattle {
	format("Now you know how battling works!")
}
